PROCESSOR	16F876A
	
	INCLUDE	<P16F876A.INC>
	STATUS_TEMP	EQU	20H
	W_TEMP	EQU	21H
	INC	EQU	22H
	INT_CNT	EQU	23H
	NOTEA	EQU	24H	;7-SEGMENT 상단(a)에 내려올 노트. 1번 bit에서 4번 bit까지 7-SEG에 표시 
	NOTEB	EQU	25H	;      / /    중단(g)
	NOTEC	EQU	26H	;      / /    하단(d)
	NOTEAS	EQU	35H	;NOTEA를 8비트 이상으로 연장
	NOTEBS	EQU	36H	;NOTEB를          / /
	NOTECS	EQU	37H	;NOTEC를          / /
	DN	EQU	27H	;눌린 버튼을 확인
	ER	EQU	28H	;실패 횟수 기록
	BT	EQU	29H	;LED 조절, 본 게임에서는 게임이 시작되면 항상 ON
	TIME	EQU	31H	;난수 생성을 위한 시간에 따른 변수
	DBUF1	EQU	33H
	DBUF2	EQU	34H
	SPEED	EQU	38H	;속도 조절을 위한 변수
	ORG	00H
REGAME
	GOTO	START_UP	;재시작 지점
	ORG	04H
	;ISR 시작 번지
	MOVWF	W_TEMP		;현재 사용되고 있는 W REG 저장
	SWAPF	STATUS, W
	MOVWF	STATUS_TEMP
	CALL	DISP		; DISPLAY 부 프로그램
	SWAPF	STATUS_TEMP, W
	MOVWF	STATUS
	SWAPF	W_TEMP, F
	SWAPF	W_TEMP, W
	BCF	INTCON,	 2
	RETFIE
				;DISPLAY ROUTINE
DISP
	INCF	INC, F		
	BTFSS	INC, 2		;8번으로 나뉘는 분기점
	GOTO	DISPA
	GOTO	DISPB
DISPA
	BTFSS	INC, 1
	GOTO	DISPAA
	GOTO	DISPAB
DISPB
	BTFSS	INC, 1
	GOTO	DISPBA
	GOTO	DISPBB
DISPAA
	BTFSS	INC, 0
	GOTO	DISP1
	GOTO	DISPLED
DISPAB
	BTFSS	INC, 0
	GOTO	DISP2
	GOTO	DISPLED
DISPBA
	BTFSS	INC, 0
	GOTO	DISP3
	GOTO	DISPLED
DISPBB
	BTFSS	INC, 0
	GOTO	DISP4
	GOTO	DISPLED
ST				;노트 표시 초기화
	BCF	PORTC, 7
	BCF	PORTA, 1
	BCF	PORTC, 1
	BCF	PORTA, 0
	RETURN
DISPLED				;LED. 본게임에서는 7-SEGMENT와 별개로 항상 ON
	BSF	PORTB, 1
	BSF	PORTA, 3
	BSF	PORTA, 2
	BSF	PORTB, 2
	BSF	PORTB, 7
	BTFSC	BT, 0
	CALL	LED1
	BTFSC	BT, 1
	CALL	LED2
	BTFSC	BT, 2
	CALL	LED3
	BCF	PORTC, 5
	BCF	PORTC, 6
	BCF	PORTC, 7
	RETURN
DISP1	
	BCF	PORTB, 7	;LED 끄기
	CALL	ST		;노트 초기화
	BTFSC	NOTEA, 4	;상단에 표시해야 할 노트 있는가?
	BSF	PORTC, 7
	BTFSC	NOTEB, 4	;중단         / /
	BSF	PORTA, 1	
	BTFSC	NOTEC, 4	;하단         / /
	BSF	PORTC, 1
	BTFSC	ER, 3		;현재 자리에 에러 표시해야 할 만큼 실패 횟수 쌓였는가?
	BSF	PORTA, 0
	BTFSC	ER, 4		;실패 5번 했는가
	CALL	FF
	BCF	PORTA, 3	;digit1번 표시
	RETURN
DISP2				;위의 작업 반복
	BCF	PORTB, 7
	CALL	ST
	BTFSC	NOTEA, 3
	BSF	PORTC, 7
	BTFSC	NOTEB, 3
	BSF	PORTA, 1	
	BTFSC	NOTEC, 3
	BSF	PORTC, 1
	BTFSC	ER, 2
	BSF	PORTA, 0
	BTFSC	ER, 4
	CALL	A
	BCF	PORTA, 2
	RETURN
DISP3	
	BCF	PORTB, 7
	CALL	ST
	BTFSC	NOTEA, 2
	BSF	PORTC, 7
	BTFSC	NOTEB, 2
	BSF	PORTA, 1	
	BTFSC	NOTEC, 2
	BSF	PORTC, 1
	BTFSC	ER, 1
	BSF	PORTA, 0
	BTFSC	ER, 4
	CALL	I
	BCF	PORTB, 2
	RETURN
DISP4	
	BCF	PORTB, 7
	CALL	ST
	BTFSC	NOTEA, 1
	BSF	PORTC, 7
	BTFSC	NOTEB, 1
	BSF	PORTA, 1	
	BTFSC	NOTEC, 1
	BSF	PORTC, 1
	BTFSC	ER, 0
	BSF	PORTA, 0
	BTFSC	ER, 4
	CALL	RR
	BCF	PORTB, 1
	BSF	PORTA, 4
	CALL	BTN
	INCF	INT_CNT
	RETURN


START_UP
	BSF	STATUS, RP0	;RAM BANK 1 선택
	MOVLW	B'00000000'	;PORT I/O 선택
	MOVWF	TRISA
	MOVLW	B'00111000'	;PORT I/O 선택
	MOVWF	TRISB
	MOVLW	B'00000000'	;PORT I/O 선택
	MOVWF	TRISC
	MOVLW	B'00000111'
	MOVWF	ADCON1
	MOVLW	B'00000000'	;INTERRUPT 시간 설정 --- 2.048msec
	MOVWF	OPTION_REG
	BCF	STATUS, RP0	;RAM BANK 0 선택
	CALL	START
	BSF	INTCON, 5	;TIMER INTERRUPT ENABLE
	BSF	INTCON, 7	;GLOBAL	INTERRUPT ENABLE
	GOTO	MAIN_ST

MAIN_ST	;
	CLRF	INT_CNT
	CLRF	ER
	CLRF	TIME
	CLRF	PORTA
	CLRF	PORTC
	CLRF	PORTB
	CLRF	NOTEA
	CLRF	NOTEB
	CLRF	NOTEC
	CLRF	DN
	CLRF	BT
	CLRF	SPEED
	BSF	PORTA, 4
	MOVLW	B'10001001'
	MOVWF	NOTEAS
	MOVLW	B'01010100'	
	MOVWF	NOTEBS
	MOVLW	B'00100010'	
	MOVWF	NOTECS
	CLRF	INT_CNT
M_LOOP				;메인 루프. 속도 설정에 따른 분기
	BTFSS	SPEED, 0
	GOTO	SPEEDA
	GOTO	SPEEDB
SPEEDA				;느린 속도. 인터럽트가 120번 일어날 때마다 노트 이동
	MOVLW	.120		
	SUBWF	INT_CNT, W
	BTFSS	STATUS, Z
	GOTO	XLOOP
	GOTO	CK_LOOP
SPEEDB				;빠른 속도. 인터럽트가 60번 일어날 때마다 XLOOP로 이동
	MOVLW	.60
	SUBWF	INT_CNT, W
	BTFSS	STATUS, Z
	GOTO	XLOOP
	GOTO	CK_LOOP
CK_LOOP
	CLRF	INT_CNT
	ADDLW	0
	RRF	NOTEAS, F	;8비트 이상을 하나의 주소처럼 사용하기 위해 동시에 변경
	RRF	NOTEA, F
	ADDLW	0		;혹시 넘어갈지 모르는 Carry 제거
	RRF	NOTEBS, F
	RRF	NOTEB, F
	ADDLW	0
	RRF	NOTECS, F
	RRF	NOTEC, F
	ADDLW	0
XLOOP
	INCF	TIME, F		;난수를 위함
	BTFSC	NOTEA, 0	;상단의 노트가 표시부를 지나왔는가(놓쳤는가)
	CALL	ERR		;실패 횟수 추가
	BTFSC	NOTEB, 0	;중단의              / /
	CALL	ERR
	BTFSC	NOTEC, 0;	;하단의              / /
	CALL	ERR
	GOTO	M_LOOP	
ERR
	CALL	RANDOMERR	;하나의 노트가 사라졌으므로 새로운 노트 생성
	BCF	NOTEA, 0
	BCF	NOTEB, 0
	BCF	NOTEC, 0
	BTFSC	ER, 3		;이미 쌓인 실패 횟수 확인
	BSF	ER, 4		;쌓인 실패 횟수에 따라 추가 기록 
	BTFSC	ER, 4
	BCF	PORTA, 4
	BTFSC	ER, 2
	BSF	ER, 3
	BTFSC	ER, 1
	BSF	ER, 2
	BTFSC	ER, 0
	BSF	ER, 1
	BSF	ER, 0
	RETURN
	
BTN
	MOVF	PORTB, W	;3개의 버튼 입력을 3~5번 비트에 저장
	MOVWF	DN
	ADDLW	0
	RRF	DN, F
	RRF	DN, F
	RRF	DN, W
	ANDLW	B'00000111'
	CALL	LU		;Look Up Table
	RETURN
LU
	ADDWF	PCL, F		;눌린 버튼 조합에 따라 실행 분기
	GOTO	S0
	GOTO	S1
	GOTO	S2
	GOTO	S3
	GOTO	S4
	GOTO	S5
	GOTO	S6
	GOTO	S7
S0				;3개의 버튼 동시에 눌림. 재시작
	CALL	REGAME	
	RETURN
S1				;왼쪽 2개 버튼 동시에 눌림. 속도 감소
	CALL	DOWN	
	RETURN
S2				;양쪽 2개 버튼 동시에 눌림. 일시정지
	CLRF	INT_CNT
	RETURN
S3				;왼쪽 끝 버튼 눌림
	BTFSC	NOTEC, 1
	CALL	RANDOM3
	BCF	NOTEC, 1
	RETURN
S4				;오른쪽 2개 버튼 동시에 눌림. 속도 증가
	CALL	UP
	RETURN
S5				;가운데 버튼 눌림
	BTFSC	NOTEB, 1
	CALL	RANDOM2
	BCF	NOTEB, 1
	RETURN
S6				;오른쪽 버튼 눌림
	BTFSC	NOTEA, 1
	CALL	RANDOM1
	BCF	NOTEA, 1
	RETURN
S7				;아무것도 눌리지 않음
	RETURN
FF				;실패했을 경우 노트 대신 FAIL이라는 글자 7-SEGMENT에 표시
	MOVLW	B'10000101'
	MOVWF	PORTC
	BCF	PORTA, 0
	BSF	PORTA, 1
	RETURN
A
	MOVLW	B'11100101'
	MOVWF	PORTC
	BCF	PORTA, 0
	BSF	PORTA, 1
	RETURN
I
	MOVLW	B'01100000'
	MOVWF	PORTC
	BCF	PORTA, 0
	BCF	PORTA, 1
	RETURN
RR
	MOVLW	B'00000111'
	MOVWF	PORTC
	BCF	PORTA, 0
	BCF	PORTA, 1
	RETURN
LED1				;조건에 따라 LED 표시. 본 게임에서는 시작되면 항상 ON
	BCF	PORTC, 7
	BSF	PORTC, 6
	BSF	PORTC, 5
	RETURN	
LED2
	BSF	PORTC, 7
	BCF	PORTC, 6
	BSF	PORTC, 5
	RETURN	
LED3
	BSF	PORTC, 7
	BSF	PORTC, 6
	BCF	PORTC, 5
	RETURN	

RANDOM1			;난수를 통한 새로운 노트 생성
	BCF	PORTA, 4	
	BCF	NOTEA, 6	;3종류의 난수를 생성하는 것은 2^n종류 난수를 생성하는 것보다
	BCF	NOTEB, 6	;어렵고, 부정확하기 때문에
	BCF	NOTEC, 6	;A 버튼을 누르면 B C 둘 중 하나를 고를 수 있도록 2종류 난수로 대체
	BTFSS	TIME, 0		;B 버튼을 누르면 C A,	C 버튼을 누르면 A B 중 하나가 나온다.
	GOTO	SB
	GOTO	SC
RANDOM2
	BCF	PORTA, 4
	BCF	NOTEA, 6
	BCF	NOTEB, 6
	BCF	NOTEC, 6
	BTFSS	TIME,0
	GOTO	SC
	GOTO	SA
RANDOM3
	BCF	PORTA, 4
	BCF	NOTEA, 6
	BCF	NOTEB, 6
	BCF	NOTEC, 6
	BTFSS	TIME, 0
	GOTO	SA
	GOTO	SB
RANDOMERR			;노트를 놓쳤을 때에도 맞췄을 때와 같이 새로운 노트를 생성해야한다.
	BCF	NOTEA, 6
	BCF	NOTEB, 6
	BCF	NOTEC, 6
	BTFSS	TIME, 0
	GOTO	SB
	GOTO	SC
SA				;새로운 노트
	BSF	NOTEA, 6
	BSF	NOTEB, 7
	RETURN
SB
	BSF	NOTEB, 6
	BSF	NOTEC, 7
	RETURN
SC
	BSF	NOTEC, 6
	BSF	NOTEA, 7
	RETURN
	
DELAY				;딜레이
	MOVLW	.255	
	MOVWF	DBUF1
LP1		
	MOVLW	.255	
	MOVWF	DBUF2
LP2
	DECFSZ	DBUF2, F
	GOTO	LP2
	DECFSZ	DBUF1, F
	GOTO	LP1
	RETURN
UP				;속도 증가
	BTFSS	SPEED, 0
	INCF	SPEED, F
	CLRF	INT_CNT
	RETURN
DOWN				;속도 감소
	BTFSC	SPEED, 0
	DECF	SPEED, F
	CLRF	INT_CNT
	RETURN
START				;게임을 시작하면 가장 먼저 3 2 1을 표시한 후 시작한다.
	BSF	PORTB, 1
	BSF	PORTA, 3
	BSF	PORTA, 2
	BSF	PORTB, 2
	MOVLW	B'11100010'
	MOVWF	PORTC
	BCF	PORTA, 0
	BSF	PORTA, 1
	BCF	PORTB, 1
	CALL	DELAY
	BSF	PORTB, 1
	MOVLW	B'11000011'
	MOVWF	PORTC
	BCF	PORTA, 0
	BSF	PORTA, 1
	BCF	PORTB, 2
	CALL	DELAY
	BSF	PORTB, 2
	MOVLW	B'01100000'
	MOVWF	PORTC
	BCF	PORTA, 0
	BCF	PORTA, 1
	BCF	PORTA, 2
	CALL	DELAY
	RETURN
	END